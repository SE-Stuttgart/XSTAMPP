<?xml version="1.0" encoding="ISO-8859-1"?>
<!-- Copyright (C) 2017 Jaqueline Patzek, Patrick Wickenhäuser,Lukas Balzer StuPro 2013 / 2014 All rights reserved. This 
    program and the accompanying materials are made available under the terms of the Eclipse Public License v1.0 which accompanies 
    this distribution, and is available at http://www.eclipse.org/legal/epl-v10.html Contributors: Jaqueline Patzek, Patrick 
    Wickenhäuser,Lukas Balzer - initial API and implementation -->
<xsl:stylesheet version="1.0" xmlns:fo="http://www.w3.org/1999/XSL/Format" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
    <!-- author: Jaqueline Patzek, Patrick Wickenhäuser,Lukas Balzer -->
    <!-- StuPro 2013 / 2014 -->

    <!-- Note: This xsl gathers all the informations from the .haz-file -->
    <!-- which is generated by the "prepareForExport()"-Command in the -->
    <!-- ViewContainer.java -->
    <xsl:import href="ucaTableTemp.xsl" />
    <xsl:param name="page.layout" select="A4" />
    <xsl:param name="page.title" select="''" />

    <xsl:template match="/*">
        <fo:root>
            <!-- Page layout -->
            <fo:layout-master-set>
                <xsl:call-template name="layout" />
            </fo:layout-master-set>
            <fo:page-sequence white-space-collapse="true" id="total">
                <xsl:attribute name="master-reference"><xsl:value-of select="$page.layout" /></xsl:attribute>

                <fo:flow flow-name="xsl-region-body">
                    <!-- Main-data for the front-page -->
                    <xsl:call-template name="SystemDescription"/>
                </fo:flow>
            </fo:page-sequence>



            <!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->
            <!-- +++++++++++++++++++++ END OF PDF +++++++++++++++++++++ -->
            <!-- ++++++++++++++++++++++++++++++++++++++++++++++++++++++ -->

        </fo:root>
    </xsl:template>

    <xsl:template name="SystemDescription">
        <fo:block intrusion-displace="line">
            <fo:table space-after="30pt" margin="5mm 5mm 10mm 5mm">
                <fo:table-column column-number="1" column-width="25%" border-style="none" />
                <fo:table-column column-number="2" column-width="75%" border-style="none" />
                <fo:table-body>
                    <fo:table-row border="1pt solid black">
                        <fo:table-cell padding="4px">
                            <fo:block font-size="12pt" font-weight="bold">
                                Title
                            </fo:block>
                        </fo:table-cell>
                        <fo:table-cell padding="4px">
                            <fo:block font-size="12pt">
                                <xsl:value-of select="projectdata/projectName" />
                            </fo:block>
                        </fo:table-cell>
                    </fo:table-row>
                    <fo:table-row border="1pt solid black">
                        <fo:table-cell padding="4px">
                            <fo:block font-size="12pt" font-weight="bold">
                                Date and Time
                            </fo:block>
                        </fo:table-cell>
                        <fo:table-cell padding="4px">
                            <fo:block font-size="12pt">
                                <xsl:value-of select="exportinformation/date" />
                            </fo:block>
                        </fo:table-cell>
                    </fo:table-row>
                    <xsl:if test="exportinformation/company">
                        <fo:table-row border="1pt solid black">
                            <fo:table-cell padding="4px">
                                <fo:block font-size="12pt" font-weight="bold">
                                    Company
                                </fo:block>
                            </fo:table-cell>
                            <fo:table-cell padding="4px">
                                <fo:block font-size="12pt">
                                    <xsl:value-of select="exportinformation/company" />
                                </fo:block>
                            </fo:table-cell>
                        </fo:table-row>
                    </xsl:if>
                    <fo:table-row border="1pt solid black">
                        <fo:table-cell padding="4px">
                            <fo:block font-size="12pt" font-weight="bold">
                                Description
                            </fo:block>
                        </fo:table-cell>
                        <fo:table-cell padding="4px">
                            <xsl:choose>
                                <xsl:when test="projectdata/rangeObjects/range">
                                    <fo:block font-size="12pt" linefeed-treatment="preserve">
                                        <xsl:for-each select="projectdata/rangeObjects/range">
                                            <xsl:choose>
                                                <xsl:when test="linebreak='true'">
                                                    <fo:block />
                                                </xsl:when>
                                                <xsl:otherwise>
                                                    <fo:inline>
                                                        <xsl:if test="foreground">
                                                            <xsl:attribute name="color"><xsl:value-of
                                                                select="foreground" /></xsl:attribute>
                                                        </xsl:if>
                                                        <xsl:if test="background">
                                                            <xsl:attribute name="background-color"><xsl:value-of
                                                                select="background" /></xsl:attribute>
                                                        </xsl:if>
                                                        <xsl:if test="decoration">
                                                            <xsl:attribute name="text-decoration"><xsl:value-of
                                                                select="decoration" /></xsl:attribute>
                                                        </xsl:if>
                                                        <xsl:attribute name="font-size"><xsl:value-of
                                                            select="fontSize" /></xsl:attribute>
                                                        <xsl:attribute name="font-weight"><xsl:value-of
                                                            select="weight" /></xsl:attribute>
                                                        <xsl:attribute name="font-style"><xsl:value-of
                                                            select="style" /></xsl:attribute>
                                                        <xsl:value-of select="descriptionPart" />
                                                    </fo:inline>
                                                </xsl:otherwise>
                                            </xsl:choose>
                                        </xsl:for-each>
                                    </fo:block>
                                </xsl:when>
                                <xsl:otherwise>
                                    <fo:block font-size="12pt">
                                        <xsl:value-of select="projectdata/projectDescription" />
                                    </fo:block>
                                </xsl:otherwise>
                            </xsl:choose>
                        </fo:table-cell>
                    </fo:table-row>
                </fo:table-body>
            </fo:table>
        </fo:block>
    </xsl:template>

</xsl:stylesheet>


